topic: ~menu()

# Concepts
concept:(want) [want "would like" "'d like" wish]
concept:(number) [one two three four five six seven eight nine ten]
concept:(propose) ["Can I suggest you" "May I ask you" "Would you like"]

concept:(hamburger) [hamburger hamburgers] $Food=Hamburger
concept:(taco) [taco tacos] $Food=Taco
concept:(wrap) [wrap wraps] $Food=Wrap
concept:(chicken) [chicken chickens] $Food=Chicken
concept:(toast) [toast toasts] $Food=Toast
concept:(pizza) [pizza pizzas] $Food=Pizza

concept:(fries) [fries] $Food=Fries
concept:(onion) ["onion rings"] $Food=Onion rings
concept:(mozzarella) ["mozzarella sticks"] $Food=Mozzarella
concept:(nuggets) [nuggets] $Food=Nuggets
concept:(wings) ["chicken wings"] $Food=Chicken wings
concept:(salad) [salad salads] $Food=Salad

concept:(coke) [coke cokes] $Food=Coke $Drink=True
concept:(sprite) [sprite sprites] $Food=Sprite $Drink=True
concept:(water) [water waters] $Food=Water $Drink=True
concept:(fanta) [fanta fantas] $Food=Fanta $Drink=True
concept:(tea) [tea teas] $Food=Tea $Drink=True
concept:(beer) [beer beers] $Food=Beer $Drink=True

concept:(cake) [cake cakes] $Food=Cake
concept:(donut) [donut donuts] $Food=Donut
concept:(milkshake) [milkshake milkshakes] $Food=Milkshake
concept:(crepes) [crepe crepes] $Food=Crepes
concept:(ice) ["ice cream" "ice creams"] $Food=Ice cream
concept:(pancake) [pancake pancakes] $Food=Pancake

concept:(food) [~hamburger ~taco ~wrap ~chicken ~toast ~pizza
                ~fries ~onion ~mozzarella ~nuggets ~wings ~salad
                ~coke ~sprite ~water ~fanta ~tea ~beer
                ~cake ~donut ~milkshake ~crepes ~ice ~pancake]

# Rules
u:({I} {~want} {[a an "a portion of" "a serving of"]} ~food {please}) Sure! I added it to your order $Number=one %additem ^enableThenGoto(sameCategory)
u:({I} {~want} _~number {["portions of" "servings of"]} ~food {please}) Sure! I added them to your order $Number=$1 %additem ^enableThenGoto(sameCategory)
u:({I} {~want} {to} checkout {please}) Ok, this is you order %goToMenu
    u1:($Drink==False) You haven't added a drink to your order. Do you want to add one?
        u2:(~yes) What drink do you want?
        u2:(~no) Your order is complete
    u1:($Drink==True) Your order is complete

# Proposals
proposal: Here are the categories %showTablet2Anim $Drink=False
proposal: %sameCategory Do you want something else from this category?
    u1:(~yes) Ok, what else do you want to order?
    u1:(~no) Fine, then I'll bring you the previous menu %goToMenu

