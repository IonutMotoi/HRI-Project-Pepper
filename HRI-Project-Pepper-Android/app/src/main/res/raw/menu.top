topic: ~menu()
# TODO Add more phrases alternatives
# TODO Add more animations
# TODO add order complete view -> next client

# Concepts
concept:(want) [want "would like" "'d like" wish]
concept:(number) [one two three four five six seven eight nine ten 1 2 3 4 5 6 7 8 9 10]
concept:(propose) ["Can I suggest you" "May I ask you" "Would you like"]
concept:(select) [see "look at" select "go to" check enter "show me" "change to" "switch to" order]
concept:(confirm) ^rand[Sure Fine Perfect Ok]
concept:(acknowledge) ^rand[Fine Ok Understood "Very well"]
concept:(add) ^rand[added placed put]
concept:(show) ^rand["Here are" "I'll show you" "These are" "I'll display you"]
concept:(request) ^rand["Do you want" "Do you wish to order" "Would you like to order"]
concept:(incomprehension) ^rand["I don't understand." "Sorry I can't understand what you said." "Can you repeat please?"]

concept:(hamburger) [hamburger hamburgers] $Food=Hamburger $View=Mains
concept:(taco) [taco tacos] $Food=Taco $View=Mains
concept:(wrap) [wrap wraps] $Food=Wrap $View=Mains
concept:(chicken) [chicken chickens] $Food=Chicken $View=Mains
concept:(toast) [toast toasts] $Food=Toast $View=Mains
concept:(pizza) [pizza pizzas] $Food=Pizza $View=Mains

concept:(fries) [fries] $Food=Fries $View=Sides
concept:(onion) ["onion rings"] $Food="Onion rings" $View=Sides
concept:(mozzarella) ["mozzarella sticks"] $Food=Mozzarella $View=Sides
concept:(nuggets) [nuggets] $Food=Nuggets $View=Sides
concept:(wings) ["chicken wings"] $Food="Chicken wings" $View=Sides
concept:(salad) [salad salads] $Food=Salad $View=Sides

concept:(coke) [coke cokes cola] $Food=Coke $View=Beverages
concept:(sprite) [sprite sprites] $Food=Sprite $View=Beverages
concept:(water) [water waters] $Food=Water $View=Beverages
concept:(fanta) [fanta fantas] $Food=Fanta $View=Beverages
concept:(tea) [tea teas] $Food=Tea $View=Beverages
concept:(beer) [beer beers] $Food=Beer $View=Beverages

concept:(cake) [cake cakes] $Food=Cake $View=Desserts
concept:(donut) [donut donuts] $Food=Donut $View=Desserts
concept:(milkshake) [milkshake milkshakes] $Food=Milkshake $View=Desserts
concept:(crepes) [crepe crepes] $Food=Crepes $View=Desserts
concept:(ice) ["ice cream" "ice creams"] $Food="Ice cream" $View=Desserts
concept:(pancake) [pancake pancakes] $Food=Pancake $View=Desserts

concept:(food) [~hamburger ~taco ~wrap ~chicken ~toast ~pizza
                ~fries ~onion ~mozzarella ~nuggets ~wings ~salad
                ~coke ~sprite ~water ~fanta ~tea ~beer
                ~cake ~donut ~milkshake ~crepes ~ice ~pancake]
concept:(someFood) [~fries ~onion ~mozzarella ~nuggets ~wings]

concept:(mains) [mains]
concept:(sides) [sides]
concept:(beverages) [beverages drinks]
concept:(desserts) [desserts cakes sweet]
concept:(category) [category submenu menu]
concept:(checkout) [checkout "check out"]


# Rules
u:({I} {~want} {[some "a portion of" "a serving of"]} ~someFood {please})  ~confirm! I ~add them to your order. $Number=one %raiseLeftHand1Anim %addItem ^enableThenGoto(sameCategory)
u:({I} {~want} {[a an "a slice of" "a portion of" "a serving of"]} ~food {please}) ~confirm! I ~add it to your order. $Number=one %raiseLeftHand1Anim %addItem ^enableThenGoto(sameCategory)
u:({I} {~want} _~number {["portions of" "servings of"]} ~food {please})~confirm I ~add! them to your order. $Number=$1 %raiseLeftHand1Anim %addItem ^enableThenGoto(sameCategory)

u:({I} {~want} {to} checkout {please}) ~confirm, ^rand["This is" "Here is" "I'll show"] your order. %goToMenu %checkout %showTablet2Anim

u:(e:Dialog/NotUnderstood) ~incomprehension %negationBothHands4Anim

u:({I} {~want} {to} {~select} {the} ~mains {~category} {please}) ~show the mains. %goToMains $View=Mains %showTablet6Anim
u:({I} {~want} {to} {~select} {the} ~sides {~category} {please}) ~show  the sides. %goToSides $View=Sides %showTablet4Anim
u:({I} {~want} {to} {~select} {the} ~beverages {~category} {please}) ~show  the beverages. %goToBeverages $View=Beverages %showTablet3Anim
u:({I} {~want} {to} {~select} {the} ~desserts {~category} {please}) ~show  the desserts. %goToDesserts $View=Desserts %showTablet2Anim
u:({I} {~want} {to} {~select} {the} ~category) Which category do you want ^rand["to see?" "I ^rand[show display] you?" "to select?"] %questionRightHand1Anim

# Next category
u:({[can could would]} {you} {go to the} next {[category menu view]} {please} $View==Mains) ~show the sides. %goToSides $View=Sides %showTablet2Anim
u:({[can could would]} {you} {go to the} next {[category menu view]} {please} $View==Sides) ~show the beverages. %goToBeverages $View=Beverages %showTablet6Anim
u:({[can could would]} {you} {go to the} next {[category menu view]} {please} $View==Beverages) ~show the desserts. %goToDesserts $View=Desserts %showTablet4Anim
u:({[can could would]} {you} {go to the} next {[category menu view]} {please} $View==Desserts) ^rand["This is" "Here is" "I'll show"] your order. %goToMenu $View=Menu %showTablet3Anim

# Previous category
u:({[can could would]} {you} ["{go} back" "{"go back to the"} previous [category menu view]"] {please} $View==Sides) ~show the mains. %goToMains $View=Mains %showTablet6Anim
u:({[can could would]} {you} ["{go} back" "{"go back to the"} previous [category menu view]"] {please} $View==Beverages) ~show the sides. %goToSides $View=Sides %showTablet4Anim
u:({[can could would]} {you} ["{go} back" "{"go back to the"} previous [category menu view]"] {please} $View==Desserts) ~show the beverages. %goToBeverages $View=Beverages %showTablet2Anim
u:({[can could would]} {you} ["{go} back" "{"go back to the"} previous [category menu view]"] {please} $View==Menu) ~show your desserts. %goToDesserts $View=Desserts %showTablet3Anim

# Proposals
proposal: Would you like me to guide you through the menu? $Drink=False %questionBothHands1Anim
    u1:([~yes "guide {me}"]) ~acknowledge, let's start from the mains. %showTablet2Anim $GuidedOrdering=True $View=Mains ^enableThenGoto(goToMains)
    u1:(~no) ~acknowledge, feel free to choose the category. %showTablet2Anim

#proposal: Here are the categories. %showTablet2Anim $Drink=False
proposal: %goToMains What do you ^rand[want wish] to order from this category?

proposal: %sameCategory ~request something else from this category?
    u1:(~yes) ~confirm, what else do you ^rand[want wish] to ^rand["order?" "add to your order?" "place into your order?"] %questionRightHand1Anim
    u1:($GuidedOrdering==True $View==Mains ~no) ~acknowledge, ~show the sides. %goToSides $View=Sides %showTablet3Anim
    u1:($GuidedOrdering==True $View==Sides ~no) ~acknowledge, ~show the beverages. %goToBeverages $View=Beverages %showTablet4Anim
    u1:($GuidedOrdering==True $View==Beverages ~no) ~acknowledge, ~show the desserts. %goToDesserts $View=Desserts %showTablet6Anim
    u1:($GuidedOrdering==True $View==Desserts ~no) ~acknowledge, then I'll ^rand[show display] what you ordered. %goToMenu $View=Menu %showTablet2Anim
    u1:(~no) ~acknowledge, then I'll ^rand[bring show display] you the previous menu. %goToMenu %raiseLeftHand1Anim %showTablet2Anim
    u1:(e:Dialog/NotUnderstood) ~incomprehension ^stayInScope %negationBothHands4Anim

proposal: %noPrompt Your order is complete. %raiseLeftHand1Anim ^topic(goodbye)

proposal: %promptMains You haven't added a main to your order. Do you want to add one? $GuidedOrdering=False %questionRightHand1Anim
    u1:(~yes) What main do you ^rand[want wish "want to order" "wish to order" "want to add" "wish to add"]? %goToMains $View=Mains %showTablet4Anim
    u1:(~no) ~acknowledge %checkout  # This time it will go to noPrompt
    u1:(e:Dialog/NotUnderstood) ~incomprehension ^stayInScope %negationBothHands4Anim

proposal: %promptSides You haven't added a side to your order. Do you want to add one? $GuidedOrdering=False %questionRightHand1Anim
    u1:(~yes) What side do you ^rand[want wish "want to order" "wish to order" "want to add" "wish to add"]? %goToSides $View=Sides %showTablet6Anim
    u1:(~no) ~acknowledge %checkout  # This time it will go to noPrompt
    u1:(e:Dialog/NotUnderstood) ~incomprehension ^stayInScope %negationBothHands4Anim

proposal: %promptBeverages You haven't added a drink to your order. Do you want to add one? $GuidedOrdering=False %questionRightHand1Anim
    u1:(~yes) What drink do you ^rand[want wish "want to order" "wish to order" "want to add" "wish to add"]? %goToBeverages $View=Beverages %showTablet3Anim
    u1:(~no) ~acknowledge %checkout  # This time it will go to noPrompt
    u1:(e:Dialog/NotUnderstood) ~incomprehension ^stayInScope %negationBothHands4Anim

proposal: %promptDesserts You haven't added a dessert to your order. Do you want to add one? $GuidedOrdering=False %questionRightHand1Anim
    u1:(~yes) What dessert ^rand[want wish "want to order" "wish to order" "want to add" "wish to add"]?%goToDesserts $View=Desserts %showTablet2Anim
    u1:(~no) ~acknowledge %checkout  # This time it will go to noPrompt
    u1:(e:Dialog/NotUnderstood) ~incomprehension ^stayInScope %negationBothHands4Anim
