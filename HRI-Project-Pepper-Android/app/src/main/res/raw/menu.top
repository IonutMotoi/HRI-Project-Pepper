topic: ~menu()
# TODO Add more phrases alternatives
# TODO Add more animations
# TODO Fix drink not ordered at checkout

# Concepts
concept:(want) [want "would like" "'d like" wish]
concept:(number) [one two three four five six seven eight nine ten]
concept:(propose) ["Can I suggest you" "May I ask you" "Would you like"]
concept:(select) [see "look at" select "go to" check enter "show me" "change to" "switch to" order]

concept:(hamburger) [hamburger hamburgers] $Food=Hamburger $View=Mains
concept:(taco) [taco tacos] $Food=Taco $View=Mains
concept:(wrap) [wrap wraps] $Food=Wrap $View=Mains
concept:(chicken) [chicken chickens] $Food=Chicken $View=Mains
concept:(toast) [toast toasts] $Food=Toast $View=Mains
concept:(pizza) [pizza pizzas] $Food=Pizza $View=Mains

concept:(fries) [fries] $Food=Fries $View=Sides
concept:(onion) ["onion rings"] $Food="Onion rings" $View=Sides
concept:(mozzarella) ["mozzarella sticks"] $Food=Mozzarella $View=Sides
concept:(nuggets) [nuggets] $Food=Nuggets $View=Sides
concept:(wings) ["chicken wings"] $Food="Chicken wing $View=Sidess"
concept:(salad) [salad salads] $Food=Salad $View=Sides

concept:(coke) [coke cokes] $Food=Coke $Drink=True $View=Beverages
concept:(sprite) [sprite sprites] $Food=Sprite $Drink=True $View=Beverages
concept:(water) [water waters] $Food=Water $Drink=True $View=Beverages
concept:(fanta) [fanta fantas] $Food=Fanta $Drink=True $View=Beverages
concept:(tea) [tea teas] $Food=Tea $Drink=True $View=Beverages
concept:(beer) [beer beers] $Food=Beer $Drink=True $View=Beverages

concept:(cake) [cake cakes] $Food=Cake $View=Desserts
concept:(donut) [donut donuts] $Food=Donut $View=Desserts
concept:(milkshake) [milkshake milkshakes] $Food=Milkshake $View=Desserts
concept:(crepes) [crepe crepes] $Food=Crepes $View=Desserts
concept:(ice) ["ice cream" "ice creams"] $Food="Ice cream" $View=Desserts
concept:(pancake) [pancake pancakes] $Food=Pancake $View=Desserts

concept:(food) [~hamburger ~taco ~wrap ~chicken ~toast ~pizza
                ~fries ~onion ~mozzarella ~nuggets ~wings ~salad
                ~coke ~sprite ~water ~fanta ~tea ~beer
                ~cake ~donut ~milkshake ~crepes ~ice ~pancake]
concept:(someFood) [~fries ~onion ~mozzarella ~nuggets ~wings]

concept:(mains) [mains]
concept:(sides) [sides]
concept:(beverages) [beverages drinks]
concept:(desserts) [desserts cakes sweet]
concept:(category) [category submenu menu]
concept:(checkout) [checkout "check out"]


# Rules
u:({I} {~want} {[some "a portion of" "a serving of"]} ~someFood {please}) Sure! I added them to your order. $Number=one %raiseLeftHand1Anim %addItem ^enableThenGoto(sameCategory)
u:({I} {~want} {[a an "a slice of" "a portion of" "a serving of"]} ~food {please}) Sure! I added it to your order. $Number=one %raiseLeftHand1Anim %addItem ^enableThenGoto(sameCategory)
u:({I} {~want} _~number {["portions of" "servings of"]} ~food {please}) Sure! I added them to your order. $Number=$1 %raiseLeftHand1Anim %addItem ^enableThenGoto(sameCategory)

u:({I} {~want} {to} checkout {please}) Ok, this is your order. %goToMenu ^enableThenGoto(checkout)

u:(e:Dialog/NotUnderstood) I don't understand.

u:({I} {~want} {to} {~select} {the} ~mains {~category} {please}) Here are the mains. %goToMains $View=Mains
u:({I} {~want} {to} {~select} {the} ~sides {~category} {please}) Here are the sides. %goToSides $View=Sides
u:({I} {~want} {to} {~select} {the} ~beverages {~category} {please}) Here are the beverages. %goToBeverages $View=Beverages
u:({I} {~want} {to} {~select} {the} ~desserts {~category} {please}) Here are the desserts. %goToDesserts $View=Desserts
u:({I} {~want} {to} {~select} {the} ~category) Which category do you want to see? %questionRightHand1Anim

# Next category
u:({[can could would]} {you} {go to the} next {[category menu view]} {please} $View==Mains) Here are the sides. %goToSides $View=Sides
u:({[can could would]} {you} {go to the} next {[category menu view]} {please} $View==Sides) Here are the beverages. %goToBeverages $View=Beverages
u:({[can could would]} {you} {go to the} next {[category menu view]} {please} $View==Beverages) Here are the desserts. %goToDesserts $View=Desserts
u:({[can could would]} {you} {go to the} next {[category menu view]} {please} $View==Desserts) Here is your order. %goToMenu $View=Menu

# Previous category
u:({[can could would]} {you} ["{go} back" "{"go back to the"} previous [category menu view]"] {please} $View==Sides) Here are the mains. %goToMains $View=Mains
u:({[can could would]} {you} ["{go} back" "{"go back to the"} previous [category menu view]"] {please} $View==Beverages) Here are the sides. %goToSides $View=Sides
u:({[can could would]} {you} ["{go} back" "{"go back to the"} previous [category menu view]"] {please} $View==Desserts) Here are the beverages. %goToBeverages $View=Beverages
u:({[can could would]} {you} ["{go} back" "{"go back to the"} previous [category menu view]"] {please} $View==Menu) Here is your desserts. %goToDesserts $View=Desserts

# Proposals
proposal: Would you like me to guide you through the menu? $Drink=False
    u1:([~yes "guide {me}"]) Ok, let's start from the mains. %showTablet2Anim $GuidedOrdering=True $View=Mains ^enableThenGoto(goToMains)
    u1:(~no) Ok, feel free to choose the category. %showTablet2Anim

#proposal: Here are the categories. %showTablet2Anim $Drink=False
proposal: %goToMains What do you want to order from this category?


proposal: %sameCategory Do you want something else from this category?
    u1:(~yes) Ok, what else do you want to order? %questionRightHand1Anim
    u1:($GuidedOrdering==True $View==Mains ~no) Fine, then I'll show you the sides. %goToSides $View=Sides
    u1:($GuidedOrdering==True $View==Sides ~no) Fine, then I'll show you the beverages. %goToBeverages $View=Beverages
    u1:($GuidedOrdering==True $View==Beverages ~no) Fine, then I'll show you the desserts. %goToDesserts $View=Desserts
    u1:($GuidedOrdering==True $View==Desserts ~no) Fine, then I'll show what you ordered. %goToMenu $View=Menu
    u1:(~no) Fine, then I'll bring you the previous menu. %goToMenu %raiseLeftHand1Anim
    u1:(e:Dialog/NotUnderstood) I don't understand. ^stayInScope

proposal: %checkout Your order is complete $Drink==True %raiseLeftHand1Anim
proposal: %checkout You haven't added a drink to your order. Do you want to add one? $Drink==False $GuidedOrdering=False %questionRightHand1Anim
    u1:(~yes) What drink do you want? %goToBeverages
    u1:(~no) Your order is complete. %raiseLeftHand1Anim
    u1:(e:Dialog/NotUnderstood) I don't understand. ^stayInScope



